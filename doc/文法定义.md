# 文法

```cpp
编译单元: CompUnit → {Decl} {FuncDef} MainFuncDef
声明: Decl → ConstDecl | VarDecl
    
常量声明: ConstDecl → 'const' BType ConstDef { ',' ConstDef } ';'
    
基本类型 BType → 'int'
    
常数定义 ConstDef → Ident { '[' ConstExp ']' } '=' ConstInitVal
    
常量表达式 ConstExp → AddExp 注：使⽤的Ident 必须是常量
    
加减表达式 AddExp → MulExp { ('+'|'-') MulExp}
    
乘除模表达式 MulExp → UnaryExp {('*' | '/' | '%') UnaryExp}
    
⼀元表达式 UnaryExp → PrimaryExp 
    		| Ident '(' [FuncRParams] ')' 
			| UnaryOp UnaryExp
    
基本表达式 PrimaryExp → '(' Exp ')' | LVal | Number

表达式 Exp → AddExp 注：SysY 表达式是int 型表达式 // 存在即可
    
左值表达式 LVal → Ident {'[' Exp ']'} //1.普通变量 2.⼀维数组 3.⼆维数组

数值 Number → IntConst
    
函数实参表 FuncRParams → Exp { ',' Exp } 

单⽬运算符 UnaryOp → '+' | '−' | '!' 注：'!'仅出现在条件表达式中

常量初值 ConstInitVal → ConstExp | '{' [ ConstInitVal { ',' ConstInitVal } ] '}'

变量声明 VarDecl → BType VarDef { ',' VarDef } ';' 
    
变量定义 VarDef → Ident { '[' ConstExp ']' } | 
    			Ident { '[' ConstExp ']' } '=' InitVal

变量初值 InitVal → Exp | '{' [ InitVal { ',' InitVal } ] '}'
    
函数定义 FuncDef → FuncType Ident '(' [FuncFParams] ')' Block 

函数类型 FuncType → 'void' | 'int' 

函数形参表 FuncFParams → FuncFParam { ',' FuncFParam } 

函数形参 FuncFParam → BType Ident ['[' ']' { '[' ConstExp ']' }] 
    // 1.普通变量2.⼀维数组变量 3.⼆维数组变量
    
主函数定义 MainFuncDef → 'int' 'main' '(' ')' Block 

语句块 Block → '{' { BlockItem } '}' 
    
语句块项 BlockItem → Decl | Stmt
    
语句 Stmt → LVal '=' Exp ';' 
    	| [Exp] ';' //有无Exp两种情况
        | Block
        | 'if' '(' Cond ')' Stmt [ 'else' Stmt ] // 1.有else 2.⽆else
        | 'while' '(' Cond ')' Stmt
        | 'break' ';' | 'continue' ';'
        | 'return' [Exp] ';' 
        | LVal = 'getint''('')'';'
        | 'printf''('FormatString{,Exp}')'';' // 1.有Exp 2.⽆Exp
    
条件表达式 Cond → LOrExp // 存在即可
    
逻辑或表达式 LOrExp → LAndExp { '||' LAndExp }

逻辑与表达式 LAndExp -> EqExp { '&&' EqExp }

关系表达式 RelExp → AddExp | RelExp ('<' | '>' | '<=' | '>=') AddExp 

相等性表达式 EqExp → RelExp | EqExp ('==' | '!=') RelExp 
```



